import org.gradle.internal.jvm.Jvm

// apply the script plugins
plugins {
    id 'java'
    id 'checkstyle'
    id 'findbugs'
    id 'com.bmuschko.nexus' version '2.3.1'
    id 'io.codearte.nexus-staging' version '0.8.0'
}

group = 'com.imsweb'
version = file('VERSION').text.trim()
description = 'Shared Swing library for the SEER Java projects'

println "Starting build process using Java ${Jvm.current()}"

repositories {
    mavenCentral()
}

// dependency jars
dependencies {
    compile 'org.swinglabs:jxlayer:3.0.4'
    compile 'org.swinglabs.swingx:swingx-painters:1.6.4'
    compile('org.xhtmlrenderer:core-renderer:R8') {
        exclude module: 'itext'
    }
    testCompile 'org.apache.commons:commons-lang3:3.6'
    testCompile 'com.imsweb:seerutils:5.3'
    testCompile 'com.imsweb:layout:1.10'
    testCompile 'com.imsweb:algorithms:1.14'
}

// enforce UTF-8 for all compilation tasks
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

// display the compilation warnings
compileJava.options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
compileTestJava.options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'

// customize the manifest
jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': version,
                'Implementation-Vendor': 'Information Management Services Inc.',
                'Created-By': System.properties['java.vm.version'] + ' (' + System.properties['java.vm.vendor'] + ')',
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
        )
    }
}

// this sucks, but they made the Javadoc way too stritct in Java 8 and it's not worth my time fixing it!
tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}

// checkstyle plugin settings
checkstyle {
    ignoreFailures = true
    configFile = file('config/checkstyle/checkstyle.xml')
}

// findbugs plugin settings
findbugs {
    ignoreFailures = true
    effort = 'max'
    excludeFilter = file('config/findbugs/findbugs-exclude.xml')
}
findbugsTest.enabled = false

// needed to deploy to Maven Central
modifyPom {
    project {
        name 'SEER*Utils GUI'
        description 'Shared Swing library for the SEER Java projects.'
        url 'https://github.com/imsweb/seerutils-gui'
        inceptionYear '2018'

        scm {
            url 'https://github.com/imsweb/seerutils-gui'
            connection 'scm:https://github.com/imsweb/seerutils-gui.git'
            developerConnection 'scm:git@github.com:imsweb/seerutils-gui.git'
        }

        licenses {
            license {
                name 'A modified BSD License (BSD)'
                url 'https://github.com/imsweb/seerutils-gui/blob/master/LICENSE'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'depryf'
                name 'Fabian Depry'
                email 'depryf@imsweb.com'
            }
        }
    }
}

// don't try to release a snapshot to a non-snapshot repository, that won't work anyway
if (version.endsWith('-SNAPSHOT'))
    gradle.startParameter.excludedTaskNames += 'closeAndReleaseRepository'

// Gradle wrapper, this allows to build the project without having to install Gradle!
task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}
