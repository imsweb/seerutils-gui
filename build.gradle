plugins {
    id 'java-library'
    id 'checkstyle'
    id 'com.github.spotbugs' version '1.6.5'
    id 'com.bmuschko.nexus' version '2.3.1'
    id 'io.codearte.nexus-staging' version '0.8.0'
}

group = 'com.imsweb'
version = file('VERSION').text.trim()
description = 'Shared Swing library for the SEER Java projects'
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

println "Starting build using JDK ${Runtime.version().feature()}"

repositories {
    mavenCentral()
}

// dependency jars
dependencies {
    testImplementation 'org.xhtmlrenderer:flying-saucer-core:9.1.16'
    testImplementation 'org.apache.commons:commons-lang3:3.7'
    testImplementation 'com.opencsv:opencsv:4.2'
    testImplementation 'com.imsweb:seerutils:5.5'
    testImplementation 'com.imsweb:layout:1.14'
    testImplementation 'com.imsweb:algorithms:1.16'
}

// enforce UTF-8, display the compilation warnings
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
}

// the Javadoc was made way too strict in Java 8 and it's not worth the time fixing everything!
tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}

// customize the manifest
jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': version,
                'Implementation-Vendor': 'Information Management Services Inc.',
                'Created-By': System.properties['java.vm.version'] + ' (' + System.properties['java.vm.vendor'] + ')',
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Automatic-Module-Name': 'com.imsweb.seerutilsgui'
        )
    }
}

// checkstyle plugin settings
checkstyle {
    ignoreFailures = true
    configFile = project(':').file('config/checkstyle/checkstyle.xml')
    configProperties = ['suppressionFile': project(':').file('config/checkstyle/suppressions.xml')]
}

// spotbugs plugin settings
spotbugs {
    ignoreFailures = true
    excludeFilter = project(':').file("config/spotbugs/spotbugs-exclude.xml")
}

// needed to deploy to Maven Central
modifyPom {
    project {
        name 'SEER*Utils GUI'
        description 'Shared Swing library for the SEER Java projects.'
        url 'https://github.com/imsweb/seerutils-gui'
        inceptionYear '2018'

        scm {
            url 'https://github.com/imsweb/seerutils-gui'
            connection 'scm:https://github.com/imsweb/seerutils-gui.git'
            developerConnection 'scm:git@github.com:imsweb/seerutils-gui.git'
        }

        licenses {
            license {
                name 'A modified BSD License (BSD)'
                url 'https://github.com/imsweb/seerutils-gui/blob/master/LICENSE'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'depryf'
                name 'Fabian Depry'
                email 'depryf@imsweb.com'
            }
        }
    }
}

// don't try to release a snapshot to a non-snapshot repository, that won't work anyway
if (version.endsWith('-SNAPSHOT'))
    gradle.startParameter.excludedTaskNames += 'closeAndReleaseRepository'

// Gradle wrapper, this allows to build the project without having to install Gradle!
wrapper {
    gradleVersion = '5.0'
    distributionType = Wrapper.DistributionType.ALL
}

